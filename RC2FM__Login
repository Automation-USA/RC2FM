#Name: RC2FM__Login History: Created by Automation USA - Julio Toledo Created: 7-3-2016 Last Modified: 5-31-2017 (added commenting, error handling/reporting) Purpose: Performs authentication against RC2FM's "login.php" endpoint with the user's RingCentral's credentials. If account is verified as an active subscriber of both RC2FM and RingCentral, establishes an REST API session (obtains token), and queries RingCentral for the user's caller id and extension information for use in making other API calls throughout the session. Parameters: RingCentral account credentials auth = contents of field "RC2FM_GLOBALS::RC_User_Name" (User's RingCentral direct number, or company's main RingCentral phone number) extension = contents of field "RC2FM_GLOBALS::RC_User_ExtNumber" (User's RingCentral Extension Number, optional) password = contents of field "RC2FM_GLOBALS::RC_User_Password" (User's RingCentral Password) Important Notes: As with RingCentral authentication, the user name may be supplied to our RC2FM server in either of two formats: User name= Main company phone number + user's extension number; or ​User name= direct phone number (no extension number requried) In either case, the correct phone number format should be E164 (numeric only). Main number + extension authentication example: User name: 13055551212 Extension: 123 Password: RC2FM_rocks! Direct Number (DID) authentication example: User name: 17865512008 Extension: [leave blank] Password: RC2FM_rocks!

#Gather required credentials
If [ IsEmpty ( Filter ( RC2FM_GLOBALS::RC_User_Name ; "0123456789+" ) ) or IsEmpty ( RC2FM_GLOBALS::RC_User_Password ) ]
	Perform Script [ “RC2FM__Fail [Internal] ( param )”; Parameter: "RingCentral user account and/or password not provided. RC2FM session not active." ]
	Exit Script [ Result: 213 ]
End If
#Gather required environment parameters
Perform Script [ “RC2FM__Subscript_Build_Session_ID” ]

#Test for modern FileMaker version
Perform Script [ “Subscript__Validate_Client_Version_≥16” ]
Set Variable [ $isModern; Value:Get ( ScriptResult ) ]

#Use cURL method for FM versions 16+
If [ $isModern ]
	#Compile authentication url
	Set Variable [ $url; Value:RC2FM_SETTINGS::RC2FM_BaseURL & "/api/login.php" ]
	Set Variable [ $curl; Value:"-d auth=" & Base64Encode ( Filter ( RC2FM_GLOBALS::RC_User_Name ; "0123456789+" ) ) & "&password=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Password ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & $$rc2fm_session_id ]
	#Remove line breaks
	Perform Script [ “Subscript__Remove_LineBreaks ( param )”; Parameter: $curl ]
	Set Variable [ $curl; Value:Get ( ScriptResult ) ]
	#Submit Login
	Insert from URL [ RC2FM_GLOBALS::API_Response__Auth; $url; cURL options: $curl ] [ Select; No dialog; Verify SSL Certificates ]
Else
	#Compile authentication url
	Set Variable [ $Auth; Value:RC2FM_SETTINGS::RC2FM_BaseURL & "/api/login.php?auth=" & Base64Encode ( Filter ( RC2FM_GLOBALS::RC_User_Name ; "0123456789+" ) ) & "&password=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Password ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & $$rc2fm_session_id ]
	#Submit Login
	Insert from URL [ RC2FM_GLOBALS::API_Response__Auth; $Auth ] [ Select; No dialog; Verify SSL Certificates ]
End If

#unlock record
Go to Field [ ] [ Select/perform ]

#process api response
If [ PatternCount ( RC2FM_GLOBALS::API_Response__Auth ; "success" ) ]
	Set Variable [ $$errorMessage; Value:"" ]
	Set Variable [ $loginResponse; Value:TrimAll ( RC2FM_GLOBALS::API_Response__Auth ; 1 ; 2 ) ]
	Perform Script [ “RC2FM__Utility__Parse_CallerIDs (rawJSON)”; Parameter: $loginResponse ]
	Perform Script [ “RC2FM__Subscript_Store_CallerIDs” ]
	Exit Script [ Result: True ]
	
Else If [ PatternCount ( RC2FM_GLOBALS::API_Response__Auth ; "error" ) ]
	Set Variable [ $errorMessage; Value:"Login error, check credentials and try again." ]
	Perform Script [ “RC2FM__Fail [Internal] ( param )”; Parameter: $errorMessage ]
	Exit Script [ Result: False ]
End If

