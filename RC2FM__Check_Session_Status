#Name: RC2FM__Check_Session_Status History: Created by Automation USA - Julio Toledo Created: 7-3-2016 Last Modified: 5-31-2017 (added branching to change the target of "Insert from URL" based on FM version) Purpose: Tests for active session (valid token) by calling RC2FM's "get_token.php" endpoint and exits with True or False script result Parameters: $$extension = RingCentral user id (obtained from "RC2FM_Login" during session initiation) $$account = RingCentral account id (obtained from "RC2FM_Login" during session initiation) RingCentral account credentials (except password): auth = contents of field "RC2FM_GLOBALS::RC_User_Name" extension = RC2FM_GLOBALS::RC_User_ExtNumber (optional) Important Notes: None

#Test for modern FileMaker version
Perform Script [ “Subscript__Validate_Client_Version_≥16” ]
Set Variable [ $isModern; Value:Get ( ScriptResult ) ]


#Post token status request
If [ $isModern ]
#Assemble token status url
Set Variable [ $token_status; Value:RC2FM_SETTINGS::RC2FM_BaseURL & "/get_token.php" ]
Set Variable [ $curl; Value:"-d auth=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Name ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&=&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & "&user_id=" & Base64Encode ( $$extension ) & "&account_id=" & Base64Encode ( $$account ) & "&persistent_id=" & Base64Encode ( Get ( PersistentID ) ) ]
#Remove line breaks
Perform Script [ “Subscript__Remove_LineBreaks ( param )”; Parameter: $curl ]
Set Variable [ $curl; Value:Get ( ScriptResult ) ]
Insert from URL [ $response; $token_status; cURL options: $curl ] [ Select; No dialog; Verify SSL Certificates ]
Set Variable [ $response; Value:TrimAll ( $response ; 1 ; 2 ) ]
Else
#Assemble token status url
Set Variable [ $token_status; Value:RC2FM_SETTINGS::RC2FM_BaseURL & "/get_token.php?auth=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Name ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&=&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & "&user_id=" & Base64Encode ( $$extension ) & "&account_id=" & Base64Encode ( $$account ) & "&persistent_id=" & Base64Encode ( Get ( PersistentID ) ) ]
Insert from URL [ RC2FM_GLOBALS::API_Response__Token_Status; $token_status ] [ Select; No dialog; Verify SSL Certificates ]
Set Variable [ $response; Value:TrimAll ( RC2FM_GLOBALS::API_Response__Token_Status ; 1 ; 2 ) ]
End If

#process api response
If [ IsEmpty ( Substitute ( TrimAll ( $response ; 1 ; 3 ) ; ¶ ; "" ) //end Substitute ) ]
Set Field [ RC2FM_GLOBALS::API_Response__Token_Status; "" ]
Exit Script [ Result: False ]
Else
Set Variable [ $parsedResponse; Value:Substitute ( $response ; [ ","; ¶ ] ; [ "{" ; ""] ; ["}" ; "" ] ; [ "\"" ; ""] ; [ "+" ; "" ] ) ]
End If

If [ PatternCount ( $parsedResponse ; "User is already active" ) ]
Exit Script [ Result: True ]
Else If [ PatternCount ( $parsedResponse ; "token expired" ) ]
Exit Script [ Result: False ]
End If
