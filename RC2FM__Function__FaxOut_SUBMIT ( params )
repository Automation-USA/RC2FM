#Name: RC2FM__Function__FaxOut_SUBMIT ( params ) History: Created by Automation USA - Julio Toledo Created: 7-3-2016 Last Modified: 6-1-2017 (added commenting, error handling/reporting, test for modern FileMaker version, cURL support) Purpose: Posts outgoing fax requests against RC2FM's "fax_base64.php" endpoint with all required authentication and payload parameters. Parameters: $$toPhone = destination fax number (E164 number as string) $$fileContainer = fully qualified field name to fax attachment container field $$fileName = fax attachment file name parameter (string) $$mimeType = fax attachment MIME type $$coverPageIndex = Fax cover page index id (optional, number 1-14 as string) $$coverPageText = Fax cover page message (optional string, only valid if $$coverPageIndex is also provided) $$extension = RingCentral user id (obtained from "RC2FM_Login" during session initiation) $$account = RingCentral account id (obtained from "RC2FM_Login" during session initiation) $$rc2fm_session_id = environment parameters (obtained from "RC2FM_Subscript_Build_Session_ID") RingCentral account credentials (except password): auth = contents of field "RC2FM_GLOBALS::RC_User_Name" extension = RC2FM_GLOBALS::RC_User_ExtNumber (optional) Important Notes: Requires a relationship path from the current context back to the fax attachment container field and RingCentral credentials fields
Allow User Abort [ Off ]

#Test for global variables
If [ IsEmpty ( $$toPhone ) or IsEmpty ( $$fileName ) or IsEmpty ( $$mimeType ) ]
	Perform Script [ “RC2FM__Fail [Internal] ( param )”; Parameter: "A Fax parameter is missing" ]
	Exit Script [ Result: 958 ]
End If

#Test for live token
Perform Script [ “RC2FM__Check_Session_Status” ]
Set Variable [ $tokenStatus; Value:Get ( ScriptResult ) ]

#Process response
If [ $tokenStatus = 0 and not IsEmpty ( RC2FM_GLOBALS::RC_User_Name ) & not IsEmpty ( RC2FM_GLOBALS::RC_User_Password ) ]
	Perform Script [ “RC2FM__Login” ]
	If [ IsEmpty ( $$extension ) ]
		Perform Script [ “RC2FM__Fail [Internal] ( param )”; Parameter: "RingCentral user account and/or password not provided.¶¶RC2FM session not active." ]
		Exit Script [ Result: "No active session and no user credentials" ]
	End If
Else If [ $tokenStatus = 0 and IsEmpty ( RC2FM_GLOBALS::RC_User_Name ) or IsEmpty ( RC2FM_GLOBALS::RC_User_Password ) ]
	Perform Script [ “RC2FM__Fail [Internal] ( param )”; Parameter: "RingCentral user account and/or password not provided.¶¶RC2FM session not active." ]
	Exit Script [ Result: "No active session and no user credentials" ]
End If

#Initialize session variables
Perform Script [ “RC2FM__Subscript_Build_Session_ID” ]

#Initialize API response global field
Set Field [ RC2FM_GLOBALS::API_Response__RC_Reply; "Loading..." ]

#Test for modern FileMaker version
Perform Script [ “Subscript__Validate_Client_Version_≥16” ]
Set Variable [ $isModern; Value:Get ( ScriptResult ) ]

#Execute FaxOut function

#Use cURL method for FM version 16+
If [ $isModern ]
	Set Variable [ $url; Value:RC2FM_SETTINGS::RC2FM_BaseURL & "/api/v1.php" ]
	#Set cURL options
	Set Variable [ $cURL_options; Value:"-d action=fax_base64&auth=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Name ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & "&user_id=" & Base64Encode ( $$extension ) & "&account_id=" & Base64Encode ( $$account ) & $$rc2fm_session_id & "&to=" & Base64Encode ( $$toPhone ) & "&faxResolution=" & Base64Encode ( "High") & "&Content-Type=" & Base64Encode ( $$mimeType ) & If ( not IsEmpty ( $$coverPageIndex ) ; "&coverIndex=" & Base64Encode ( $$coverPageIndex ) ; "&coverIndex=" & Base64Encode ( "0" ) ) & If ( not IsEmpty ( $$coverPageText ) and not IsEmpty ( $$coverPageIndex ); "&coverPageText=" & Base64EncodeRFC ( 4648 ; $$coverPageText) ; "" ) & "&attachment_filename=" & Base64Encode ( $$fileName ) & "&attachment_content=" ]
	#Remove line breaks
	Perform Script [ “Subscript__Remove_LineBreaks ( param )”; Parameter: $cURL_options ]
	#Append binary and POST
	Set Variable [ $cURL_options; Value:Get ( ScriptResult ) & Base64EncodeRFC ( 4648 ; GetField ( $$fileContainer ) ) ]
	#POST
	Insert from URL [ RC2FM_GLOBALS::API_Response__RC_Reply; $url; cURL options: $cURL_options ] [ Select; No dialog; Verify SSL Certificates ]
	#Clean up response
	Set Field [ RC2FM_GLOBALS::API_Response__RC_Reply; Substitute ( RC2FM_GLOBALS::API_Response__RC_Reply ; "\/" ; "/" ) ]
	Go to Field [ ] [ Select/perform ]
Else If [ PatternCount( Get ( ApplicationVersion ) ; "Web" ) //legacy PlatformWeb ]
	#Use Insert from URL method on WebDirect platform (all previous versions)
	Set Variable [ $url; Value:RC2FM_SETTINGS::RC2FM_BaseURL & "/api/v1.php?action=fax_base64&auth=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Name ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & "&user_id=" & Base64Encode ( $$extension ) & "&account_id=" & Base64Encode ( $$account ) & $$rc2fm_session_id & "&to=" & Base64Encode ( $$toPhone ) & "&faxResolution=" & Base64Encode ( "High") & "&Content-Type=" & Base64Encode ( $$mimeType ) & If ( not IsEmpty ( $$coverPageIndex ) ; "&coverIndex=" & Base64Encode ( $$coverPageIndex ) ; "&coverIndex=" & Base64Encode ( "0" ) ) & If ( not IsEmpty ( $$coverPageText ) and not IsEmpty ( $$coverPageIndex ); "&coverPageText=" & Substitute ( Base64Encode ( $$coverPageText ); [Char ( "10" ); ""]; [Char ( "13" ); ""]; [Char ( "¶" ); ""] ) ; "" ) & "&attachment_filename=" & Base64Encode ( $$fileName ) & "&attachment_content=" & Substitute ( Base64Encode ( GetField ( $$fileContainer ) ) ;[Char ( "10" ); ""]; [Char ( "13" ); ""]; [Char ( "¶" ); ""] ) ]
	Insert from URL [ RC2FM_GLOBALS::API_Response__RC_Reply; $url ] [ Select; No dialog; Verify SSL Certificates ]
Else
	#Use AJAX form method in WebViewer (all previous FM versions)

	#Capture host IP for AJAX response
	Set Variable [ $FileHost; Value:Case ( Get ( HostIPAddress ) = "" ; "$"; // File is local Get ( HostIPAddress ) ) ]

	#Substitute variables in AJAX template
	Set Field [ RC2FM_GLOBALS::API_Functions_Utility_field; "data:text/html," & Substitute ( RC2FM_SETTINGS::RC_HtmlAjaxTemplate; ["{{URL}}"; RC2FM_SETTINGS::RC2FM_BaseURL & "/api/v1.php?action=fax_base64"]; ["{{FORM_DATA}}"; "auth=" & Base64Encode ( RC2FM_GLOBALS::RC_User_Name ) & If ( not IsEmpty ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "&extension=" & Base64Encode ( RC2FM_GLOBALS::RC_User_ExtNumber ) ; "" ) & "&user_id=" & Base64Encode ( $$extension ) & "&account_id=" & Base64Encode ( $$account ) & $$rc2fm_session_id & "&to=" & Base64Encode ( $$toPhone ) & "&faxResolution=" & Base64Encode ( "High") & "&Content-Type=" & Base64Encode ( $$mimeType ) & "&coverIndex=" & If ( not IsEmpty ( $$coverPageIndex ) ; "&coverIndex=" & Base64Encode ( $$coverPageIndex ) ; "&coverIndex=" & Base64Encode ( "0" ) ) & "&coverPageText=" & If ( not IsEmpty ( $$coverPageText ) and not IsEmpty ( $$coverPageIndex ); "&coverPageText=" & Substitute ( Base64Encode ( $$coverPageText); [Char ( "10" ); ""]; [Char ( "13" ); ""]; [Char ( "¶" ); ""] ) ; "" ) & "&attachment_filename=" & Base64Encode ( $$fileName ) & "&attachment_content=" & Substitute ( Base64Encode ( GetField ( $$fileContainer ) ) ;[Char ( "10" ); ""]; [Char ( "13" ); ""]; [Char ( "¶" ); ""] )]; ["{{FILE_HOST}}"; $FileHost]; ["{{FILE_NAME}}"; Get( FileName )] ) ]

	#Submit AJAX form to WebViewer
	Allow User Abort [ On ]
	Set Web Viewer [ Object Name: "ajax_web_viewer"; URL: RC2FM_GLOBALS::API_Functions_Utility_field ]
	Go to Field [ ] [ Select/perform ]
End If

#Clean up
Set Field [ RC2FM_GLOBALS::FaxOut_Utility_Container; "" ]
#Reset FaxOut global variables
Set Variable [ $$toPhone; Value:"" ]
Set Variable [ $$fileName; Value:"" ]
Set Variable [ $$mimeType; Value:"" ]
Set Variable [ $$fileContainer; Value:"" ]
Set Variable [ $$coverPageIndex; Value:"" ]
Set Variable [ $$coverPageText; Value:"" ]
